buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'http://repository.jboss.org/nexus/content/groups/public/' }
        maven { url 'https://repo.spring.io/plugins-release' }
        jcenter()
    }
    dependencies {
        classpath 'org.hibernate.build.gradle:gradle-maven-publish-auth:2.0.1'
        classpath 'com.netflix.nebula:gradle-ospackage-plugin:3.4.0'
        classpath 'org.hidetake:gradle-ssh-plugin:1.0.5'
        classpath 'de.undercouch:gradle-download-task:3.1.1'
    }
}

apply plugin: 'maven-publish'
apply plugin: 'maven-publish-auth'
apply plugin: 'nebula.ospackage'
apply plugin: 'org.hidetake.ssh'
apply plugin: 'de.undercouch.download'
apply plugin: 'signing'

group = 'org.openrepose'
def releaseNum = '6'

import de.undercouch.gradle.tasks.download.Download

def flumeDir = "$buildDir/apache-flume-$flumeVersion-bin"
def flumeArchive = "${flumeDir}.tar.gz"
def flumeDistroUrl = "http://www.apache.org/dist/flume/$flumeVersion/apache-flume-$flumeVersion-bin.tar.gz"

task downloadFlume(type: Download) {
    src flumeDistroUrl
    dest buildDir
}

task unpackFlume(dependsOn: downloadFlume, type: Copy) {
    from tarTree(resources.gzip(flumeArchive))
    into buildDir

    // to support running flume as a service, write logs to absolute path, standard location
    filesMatching('**/log4j.properties') {
        filter { line -> line.contains('flume.log.dir=./logs') ? 'flume.log.dir=/var/log/flume' : line }
    }
}

def app_user = 'flume'
def app_group = 'flume'
def log_path = '/var/log/flume'

publishing {
    publications {
        mavenRpm(MavenPublication) {
            artifactId 'flume-repose-rpm'
            version "$flumeVersion-$releaseNum"
            artifact source: buildRpm, extension: 'rpm'
        }
        mavenDeb(MavenPublication) {
            artifactId 'flume-repose-deb'
            version "$flumeVersion-$releaseNum"
            artifact source: buildDeb, extension: 'deb'
        }
    }
    repositories {
        maven {
            name 'releases.maven.research.rackspace.com'
            url 'https://maven.research.rackspacecloud.com/content/repositories/releases'
        }
    }
}

task('deployFlume', dependsOn: ['buildRpm', 'buildDeb']) << {
    ssh.run {
        session(remotes.packageRepo) {
            put from: buildRpm.outputs.files, into: '/home/repose-dev/RELEASES'
            put from: buildDeb.outputs.files, into: '/home/repose-dev/RELEASES'
        }
    }
}

ospackage {
    packageName = 'flume-repose'
    packageDescription = 'Installs Flume with Repose recommended settings.'
    version = flumeVersion
    os = LINUX

    priority = 'optional'
    packageGroup = 'Applications/Internet'
    maintainer = 'Repose Development <repose-development@lists.openrepose.org>'
    url = 'http://openrepose.org'

    if(project.hasProperty('signing.keyId') && project.hasProperty('signing.password') && project.hasProperty('signing.secretKeyRingFile')) {
        signingKeyId project.getProperty('signing.keyId')
        signingKeyPassphrase project.getProperty('signing.password')
        signingKeyRingFile file(project.getProperty('signing.secretKeyRingFile'))
    }

    preInstall = file("$parent.projectDir/preInstall.sh")

    user = app_user
    permissionGroup = app_group

    from flumeDir
    into '/opt/flume'

    from('src/main/resources/sysconfig') {
        into '/etc/sysconfig'
        include 'flume-ng'
        user 'root'
        permissionGroup 'root'
        fileMode 0644
    }

    from('src/main/resources/default/flume-ng') {
        into '/etc/default'
        include 'flume-ng'
        user 'root'
        permissionGroup 'root'
        fileMode 0644
    }

    // looks like its not straightforward to create a directory with no files.  This works, though.
    // https://github.com/nebula-plugins/gradle-ospackage-plugin/issues/14
    postInstall "mkdir -p $log_path && chown -R $app_user:$app_group $log_path"
}

buildRpm {
    release = releaseNum

    addParentDirs false
    from('src/main/resources/init.d/rpm') {
        into '/etc/init.d'
        include 'flume-ng'
        user 'root'
        permissionGroup 'root'
        fileMode 0755
    }
}

buildDeb {
    from('src/main/resources/init.d/deb') {
        into '/etc/init.d'
        include 'flume-ng'
        user 'root'
        permissionGroup 'root'
        fileMode 0755
    }
}

buildRpm.dependsOn unpackFlume
buildDeb.dependsOn unpackFlume

apply from: '../remotesConfig.gradle'

tasks.build.dependsOn 'buildRpm', 'buildDeb'
